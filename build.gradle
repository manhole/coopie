import java.time.Instant
import java.time.temporal.ChronoUnit

plugins {
    id "com.github.hierynomus.license" version "0.11.0"
}

allprojects { project ->
    group = "jp.sourceforge.hotchpotch"
    version = "0.5.0-SNAPSHOT"

    repositories {
        mavenCentral()
        maven { url "https://hotchpotch.osdn.jp/maven2/release" }
    }

    apply from: "${project.getRootDir()}/gradle/eclipse.gradle"
    apply plugin: "license"
    license {
        header rootProject.file("LICENSE-coopie.txt")
        strictCheck true
        useDefaultMappings false
        mapping {
            java = "SLASHSTAR_STYLE"
            groovy = "SLASHSTAR_STYLE"
        }
    }
}

// https://qiita.com/cypher256/items/f3db6bba86692c3c7733
// @see org.gradle.api.tasks.wrapper.Wrapper, org.gradle.util.DistributionLocator
wrapper {
    distributionType = Wrapper.DistributionType.ALL
    doFirst {
        def versionService = new URL('https://services.gradle.org/versions/current')
        gradleVersion = new groovy.json.JsonSlurper().parseText(versionService.text).version
        println "gradleVersion: ${gradleVersion}"
    }
}

final timestamp = Instant.now()

subprojects { project ->
    apply plugin: "java"
    apply plugin: "maven-publish"

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    tasks.withType(SourceTask)
            .findAll { t -> t.hasProperty('options') }
            .findAll { t -> t.options.hasProperty('encoding') }
            .each { t -> t.options.encoding = 'UTF-8' }

    tasks.register('sourcesJar', Jar) {
        dependsOn classes
        classifier = "sources"
        from sourceSets.main.allSource
    }

    tasks.register('buildNumberTask') {
        doLast {
            def gitCommand = "git rev-parse --verify --short=8 HEAD"
            def p = gitCommand.execute(null, project.getRootDir())
            def w = new StringBuilder()
            p.waitForProcessOutput(w, System.err)
            if (p.exitValue() != 0) {
                throw new Exception("execution failed: <${gitCommand}>")
            }
            def buildNumber = w.toString().trim()
            assert buildNumber
            project.ext.buildNumberValue = buildNumber
        }
    }

    tasks.withType(Jar).configureEach {
        dependsOn buildNumberTask
        doFirst {
            manifest {
                attributes["Archiver-Version"] = "Gradle ${gradle.gradleVersion}"
                attributes["Created-By"] = "Gradle"
                attributes["Built-By"] = "${System.getProperty("user.name")}"
                attributes["Build-Jdk"] = "${System.getProperty("java.version")} (${System.getProperty("java.vm.vendor")} ${System.getProperty("java.vm.version")})"
                attributes["Specification-Title"] = project.name
                attributes["Specification-Version"] = project.version
                //attributes["Specification-Vendor"] = project.organization.name
                attributes["Implementation-Title"] = project.name
                attributes["Implementation-Version"] = project.version
                attributes["Implementation-Vendor-Id"] = project.group
                //attributes["Implementation-Vendor"] = project.organization.name
                attributes["Built-Timestamp"] = timestamp.truncatedTo(ChronoUnit.SECONDS).toString()
                attributes["SCM-Revision"] = project.ext.buildNumberValue
            }
        }
    }

    /*
     * jarを作る前にbuildNumberを実行したい。
     * jarタスクのdoLastのタイミングでは遅い。(既にjarが作られた後。)
     * doFirstでも、先にjarを作る処理が動いてしまっている。
     *
     * jar, sourcesJarを対象。
     */
    tasks.withType(Jar).each { it.dependsOn buildNumberTask }

    artifacts { archives sourcesJar }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
                artifact sourcesJar
                pom.withXml { /* org.gradle.api.XmlProvider */ XmlProvider xml ->
                    /*
                     * "maven-publish"が用意する<dependencies>を破棄して、作り変える。
                     * 1. compile scopeがruntimeになってしまう。
                     * 2. optionalを指定できない。
                     * 3. maven-publishが作成するpomのscopeはcompileだけのようだ。test scopeなども加えたい。
                     */

                    def dependencyList = new DependencyList()
                    project.configurations.each { Configuration config ->
                        config.dependencies.each { Dependency dependency ->
                            dependencyList.add(config, dependency)
                        }
                    }

                    // groovy.util.Node
                    Node projectNode = xml.asNode()
                    Node dependencies = projectNode.dependencies[0]
                    // dependenciesを作りなおす
                    dependencies.children().clear()
                    Node dummyNode = dependencies.appendNode('dummy')
                    dummyNode + {
                        NodeBuilder dependenciesBuilder = delegate
                        dependencyList.holders.each { String key, DependencyHolder holder ->
                            dependenciesBuilder.dependency {
                                resolveStrategy = Closure.DELEGATE_FIRST
                                groupId holder.groupId()
                                artifactId holder.artifactId()
                                version holder.version()
                                // pom.xmlから<scope>compile</scope>を省略する。
                                if ("compile" != holder.mavenScope()) {
                                    scope holder.mavenScope()
                                }
                                if (holder.classifier()) {
                                    type holder.classifier()
                                }
                                if (holder.isOptional()) {
                                    optional true
                                }
                                if (holder.dependency.excludeRules) {
                                    exclusions {
                                        resolveStrategy = Closure.DELEGATE_FIRST
                                        holder.dependency.excludeRules.each { excludeRule ->
                                            exclusion {
                                                delegate.artifactId excludeRule.module
                                                delegate.groupId excludeRule.group
                                            }
                                        }
                                    }
                                }
                            }
                        }
                    }
                    dependencies.remove(dummyNode)

                    // http://maven.apache.org/ref/3.0.5/maven-model/maven.html
                    // dependenciesの前(versionの後ろ)へ追加したい
                    Node versionNode = projectNode.version[0]
                    versionNode + {
                        resolveStrategy = Closure.DELEGATE_FIRST

                        packaging "jar"
                        name project.name
                        description project.description
                        url "https://github.com/manhole/coopie"
                        inceptionYear 2010
                        licenses {
                            license {
                                name "The Apache Software License, Version 2.0"
                                url "http://www.apache.org/licenses/LICENSE-2.0.txt"
                                distribution "repo"
                            }
                        }
                        developers {
                            developer {
                                id "manhole"
                                name "HONMA Hirotaka"
                                email "manholex@gmail.com"
                            }
                        }
                        scm {
                            connection "scm:git:git@github.com:manhole/coopie.git"
                            developerConnection "scm:git:git@github.com:manhole/coopie.git"
                            url "https://github.com/manhole/coopie"
                        }
                    }
                }
            }
        }
        repositories {
            if (!project.version.endsWith("-SNAPSHOT")) {
                maven {
                    name "coopieLocal"
                    url "file:${rootDir}/../site/maven2/release"
                }
            }
        }
    }
}

def versions = [slf4j: "2.0.9", logback: "1.3.11"]

project(":coopie") {
    description = "Read/Write library for two-dimensional arrays of data."

    dependencies {
        implementation("org.slf4j:slf4j-api:${versions.slf4j}")
        implementation("org.apache.poi:poi-ooxml:3.9") {
            ext.optional = true
        }
        testImplementation("ch.qos.logback:logback-core:${versions.logback}")
        testImplementation("ch.qos.logback:logback-classic:${versions.logback}")
        testImplementation 'junit:junit:4.13.2'
        testImplementation 'org.hamcrest:hamcrest:2.2'
    }
}

project(":coopie-groovy") {
    description = "easy to use coopie from Groovy"
    apply plugin: "groovy"

    dependencies {
        implementation("org.slf4j:slf4j-api:${versions.slf4j}")
        implementation("org.apache.groovy:groovy:4.0.15")
        implementation(project(":coopie"))
        testImplementation("ch.qos.logback:logback-core:${versions.logback}")
        testImplementation("ch.qos.logback:logback-classic:${versions.logback}")
        testImplementation 'junit:junit:4.13.2'
        testImplementation 'org.hamcrest:hamcrest:2.2'
    }
}

class DependencyList {

    def holders = [:]

    def add(Configuration config, Dependency dependency) {
        def holder = new DependencyHolder(dependency: dependency, configuration: config)
        def prev = holders[holder.key()]
        holders[holder.key()] = holder

        assert prev == null
    }

    DependencyHolder remove(String key) {
        println "remove key=" + key
        return holders.remove(key)
    }

}

class DependencyHolder {
    Dependency dependency
    Configuration configuration

    def key() {
        // groupId + ":" + artifactId
        "${dependency.group}:${dependency.name}"
    }

    def groupId() {
        dependency.group
    }

    def artifactId() {
        dependency.name
    }

    def version() {
        dependency.version
    }

    def classifier() {
        def artifact = dependency.artifacts.find { DependencyArtifact artifact ->
            if (artifact.classifier) {
                return true
            }
        }
        if (artifact) {
            return mavenClassifier(artifact)
        }
        return null
    }

    def isOptional() {
        if (dependency.ext.has("optional") && dependency.optional) {
            return true
        }
        return false
    }

    def mavenScope() {
        if (dependency.ext.has('mavenScope')) {
            return dependency.mavenScope
        }
        switch (configuration.name) {
            case 'implementation':
                return 'compile'
            case 'testImplementation':
                return 'test'
        }
        return configuration.name
    }

    private mavenClassifier(DependencyArtifact artifact) {
        if ('tests' == artifact.classifier) {
            return 'test-jar'
        }
        return artifact.classifier
    }

}
